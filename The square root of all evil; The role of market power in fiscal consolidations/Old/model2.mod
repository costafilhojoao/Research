%What if? The role of market power in the Portuguese twin recessions
%This is a simple homogenous-agent (semi-small) open-economy model 
%with an endogenous markup generated by dynamic demand à la Santos et al. (2021)
% Luís Costa & João Costa Filho developed it to submit to the PEJ conference 2021. 
%We can complicate it later.
%Assumptions: discrete time, infinitely-living agents, stocks measured at the begining of the period, etc.
%Correspondence: João Ricardo Costa Filho; joao.costa@fgv.br

close all;

%--------------------------------------------------------------------------------------------------------------------------------------
% 1. Data
%--------------------------------------------------------------------------------------------------------------------------------------

dgov   = xlsread('data','Sheet1',['B','2',':','B','101']);   %observed Log-detrended government spending 

%--------------------------------------------------------------------------------------------------------------------------------------
% 1. Defining variables
%--------------------------------------------------------------------------------------------------------------------------------------

%Endogenous variables
var C, H, P, lambda, W, B, PI, T, G, p, MC, y, e, A, mu, Y, X, i;

% C      :  aggregate real consumption
% H      :  aggregate hours of work
% P      :  aggregate price index,
% lambda :  households' Hamiltonian multiplier
% W      :  wage rate
% B      :  stock of net assets
% PI     :  aggregate profits
% T      :  lump-sum tax
% G      :  aggregate real government consumption
% p      :  individual price
% MC     :  individual marginal cost,
% theta  :  firms' Hamiltonian multiplier
% y      :  individual output
% e      :  demand shifter
% A      :  productivity
% mu     :  individual markup
% Y      :  aggregate real output, as value added
% X      :  aggregate real net exports
% i      :  nominal interest rate

%Exogenous variables
varexo sA sG, sX, si; % sunspot; % sX; 

% sA : productivity shock
% sG : government spending shock
% si : interest rate shock
% sX : net exports shock

parameters 
psi sigma alpha phi eta gamma omega rho zetaA zetaG;

%--------------------------------------------------------------------------------------------------------------------------------------
% 2. Calibration
%--------------------------------------------------------------------------------------------------------------------------------------
sigma = 4.319094694;                              % price-elasticity of demand 
alpha = 1/3;                              % 1 - alpha: share of labor in the production
phi   = 0;                             % future demand sensitivity to current sales
eta   = 0;                              % demand ``loss"
rho   = ( ( 1 + 0.09 )^( 1 / 4 ) ) - 1 ;  % discount rate 
gamma = 2;                                % elasticity of intertemporal substitution
omega = 1.455;                            % exponent of labor in utility function 
zetaA = 0.9;                              % technology process autoregressive weight
zetaG = 0.9;                              % government spending process autoregressive weight
psi   = 1.480331915;                      % demand

%--------------------------------------------------------------------------------------------------------------------------------------
% 3. Model 
%--------------------------------------------------------------------------------------------------------------------------------------

model; 

#Gbar = .1916550811;
#Abar = .8818154044;

%%%%% Household block
% eq. () - FOC for consumption
( C - ( H ^ omega ) / omega ) ^ ( - gamma ) = lambda * P;

%eq. () - FOC for labour (hours)
H ^ ( omega - 1 ) * (  ( C - ( H ^ omega ) / omega ) ^ ( - gamma ) ) = W * lambda;

%eq. () - Euler equation
lambda(+1) * ( 1 +  i ) = ( 1 + rho )  * lambda;

% eq. () - Budget constraint 
B = ( 1 + i(-1) ) * B(-1) + W * H + PI - T - P * C;

%%%%% Government block
% eq. () - Balanced budget (Ricardian equivalence)
P * G = T;

% eq. () - Exogenous process of government spending
log( G ) = ( 1 - zetaG) * log( Gbar ) + zetaG * log ( G(-1) ) + sG;

%%%%% Rest of the world block

% eq. () - Exogenous process of net exports
%log( X ) = ( 1 - zetaX) * 0 + zetaX * log ( X(-1) ) + sX;
X = 0 + sX;
 
% eq. () - Exogenous process of nominal interest rates 
i = rho + si;
%i = rho + exp( -B) - 1 + si;

%%%%% Firms block

%eq. () - pricing equation
( ( sigma - 1 ) / sigma ) * p = MC;

%eq. () - Marginal cost
MC = ( W / ( ( 1 - alpha ) * A ^ ( 1 / ( 1 - alpha ) ) ) ) * y ^ ( alpha / ( 1 - alpha ) )  ;

%eq. () - Individual markup
mu = p / MC;

%eq. () - Law of motion for the demand shifter
e =  1 / psi;

%eq. () - Exogenous process of productivity
log( A ) = ( 1 - zetaA) * log( Abar ) + zetaA * log ( A(-1) ) + sA;

%%%%% Aggregation block

%eq. ()
P = ( psi * e(-1) ) ^( -1 / ( sigma - 1 ) ) * p;

%eq. ()
Y = ( psi * e(-1) ) ^( 1 / ( sigma - 1 ) ) * A * H ^ ( 1 - alpha );

%eq. ()
Y = C + G + X;

%eq. ()
PI = P * Y - W * H;

%eq. ()
y = ( ( psi * e(-1) ) ^( - 1 / ( sigma - 1 ) ) ) * Y;

end;
%--------------------------------------------------------------------------------------------------------------------------------------

%--------------------------------------------------------------------------------------------------------------------------------------
% 4. Steady State
%--------------------------------------------------------------------------------------------------------------------------------------

steady_state_model;
%initval;

X      = 0;
B      = 0;
i      = rho;
e      = 1 / psi;
P      = 1;
p      = P;
theta  = .1510634469;
MC     = .7684699990; 
mu     = 1.301286975;
A      = .8818154044;
Y      = .4503494519;
y      = Y;
G      = .1916550811;
T      = G * P;
C      = .2586943708;
W      = .6321605071;
PI     = .2196294234;
lambda = 99.75265100;
H      = 1865 /( 365 * 14 );

end;

steady;
%resid;

%--------------------------------------------------------------------------------------------------------------------------------------
% 5. Simulation
%--------------------------------------------------------------------------------------------------------------------------------------

shocks;

var sG;
periods 1:100;
values (dgov);
end;

simul(periods=100);

%perfect_foresight_setup(periods=100);
%perfect_foresight_solver;